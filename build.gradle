plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'
}

version="1.7.1"
description="${project.name} build script"
group="fastily"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    jcenter()
}

dependencies {
    api "com.google.code.gson:gson:2.8.6"
    api "com.squareup.okhttp3:okhttp:4.2.2"
    
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    //testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.5.2'
    
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.2.2'
}

test {
	useJUnitPlatform()
}


javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/en/java/javase/11/docs/api/"
        links "https://square.github.io/okio/1.x/okio/"
        links "https://square.github.io/okhttp/3.x/okhttp/"
        links "https://fastily.github.io/jwiki/docs/gson/"
        
        // links "http://square.github.io/okhttp/3.x/mockwebserver/"
        // setOutputLevel JavadocOutputLevel.VERBOSE
    }
}


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

wrapper {
    gradleVersion = '6.0.1'
}


publishing {
    publications {
        mavenPublish(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact packageJavadoc

            pom {
                name = 'jwiki'
                description = 'A library for effortlessly interacting with Wikipedia/MediaWiki.'
                inceptionYear = '2014'
                url = 'https://github.com/fastily/jwiki'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}