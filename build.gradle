plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'
}

version="1.6.1"
description="${project.name} build script"
group="fastily"

sourceCompatibility = 1.8
targetCompatibility = 1.8
    
repositories {
    jcenter()
}

dependencies {
    api "com.google.code.gson:gson:2.8.5"
    api "com.squareup.okhttp3:okhttp:3.11.0"
   
    //slf4j
    compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.2'
    compile group: 'org.slf4j', name:'slf4j-log4j12', version: '1.7.25'
    
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.3.1'
    
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.11.0'
}

test {
	useJUnitPlatform()
}


javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/javase/8/docs/api/"
        links "https://square.github.io/okio/1.x/okio/"
        links "https://square.github.io/okhttp/3.x/okhttp/"
        links "https://fastily.github.io/jwiki/docs/gson/"
        
        // links "http://square.github.io/okhttp/3.x/mockwebserver/"
        // setOutputLevel JavadocOutputLevel.VERBOSE
    }
}


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

wrapper {
    gradleVersion = '4.10.2'
}


publishing {
    publications {
        mavenPublish(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact packageJavadoc

            pom {
                name = 'jwiki'
                description = 'A library for effortlessly interacting with Wikipedia/MediaWiki.'
                inceptionYear = '2014'
                url = 'https://github.com/fastily/jwiki'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}